buildscript {
    repositories {
        mavenLocal()
        maven{
            url = "https://oss.sonatype.org/content/groups/public"
        }
        mavenCentral()
    }
    dependencies {
        classpath group: 'com.github.acsgh.common.gradle', name: 'gradle-plugins', version: '0.0.13', changing: true
    }
}

allprojects {
    group = 'com.github.acsgh.spark.scala'

    apply plugin: 'acsgh-version'
}

println("Version: " + version)

subprojects {
    apply plugin: 'acsgh-common-java'


    dependencies {
        compile "org.scala-lang:scala-library:$SCALA_VERSION"
        compile "org.slf4j:slf4j-api:1.7.21"

        testRuntime 'ch.qos.logback:logback-classic:1.1.7'

        testCompile "org.junit.jupiter:junit-jupiter-api:5.3.2"
        testCompile "org.junit.jupiter:junit-jupiter-params:5.3.2"
        testRuntime "org.junit.jupiter:junit-jupiter-engine:5.3.2"
        testCompile 'org.junit.vintage:junit-vintage-engine:5.3.2'
        testCompile 'junit:junit:4.12'

        testCompile "org.scalatest:scalatest_2.12:3.0.5"
        testCompile 'org.scalacheck:scalacheck_2.12:1.14.0'
        testRuntime "org.pegdown:pegdown:1.4.2"
        testCompile "org.scalamock:scalamock_2.12:4.1.0"
    }
}

def customizePom(pom) {
    pom.withXml {
        def root = asNode()

        // add all items necessary for maven central publication
        root.children().last() + {
            resolveStrategy = Closure.DELEGATE_FIRST

            name 'spark-scala'
            description 'Common utils used in several projects'
            url 'https://github.com/acsgh/spark-scala'

            licenses {
                license {
                    name 'Apache License 2.0'
                    url 'https://github.com/acsgh/spark-scala/blob/master/LICENSE'
                    distribution 'repo'
                }
            }

            developers {
                developer {
                    id "acsgh"
                    name 'Alberto Crespo'
                    email 'albertocresposanchez@gmail.com'
                }
            }

            scm {
                connection "scm:git:https://github.com/acsgh/spark-scala"
                developerConnection "scm:git:https://github.com/acsgh/spark-scala"
                url "https://github.com/acsgh/spark-scala"
            }
        }
    }
}

project.extensions.add("customizePom", this.&customizePom)

